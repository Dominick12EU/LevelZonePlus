import org.gradle.jvm.toolchain.JvmVendorSpec

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = 'it.dominick'
description = 'LevelZonePlus'
version = '2.0'

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

repositories {
    mavenCentral()
    maven { name = 'spigotmc-repo'; url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { name = 'sonatype'; url = 'https://oss.sonatype.org/content/groups/public/' }
    maven { url = 'https://maven.enginehub.org/repo/' }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT"
    compileOnly "org.projectlombok:lombok:1.18.24"
    compileOnly "com.sk89q.worldguard:worldguard-bukkit:7.0.0"
    compileOnly files("libs/AlonsoLevels-v2.3.2-BETA.jar")

    implementation "dev.triumphteam:triumph-gui:3.1.2"

    annotationProcessor "org.projectlombok:lombok:1.18.24"
}


processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') { expand props }
}

shadowJar {
    minimize()
    archiveFileName = "${project.description}-${project.version}.jar"
    relocate 'dev.triumphteam', 'it.dominick.lzp.libs'
}

tasks.build.finalizedBy tasks.runServer

tasks.named('runServer') {
    minecraftVersion '1.17.1'
    runDirectory.set(layout.projectDirectory.dir('run'))

    javaLauncher = javaToolchains.launcherFor {
        vendor = JvmVendorSpec.JETBRAINS
        languageVersion = JavaLanguageVersion.of(17)
    }
    jvmArgs(
            '-Xmx2G',
            '-XX:+UseG1GC',
            '-XX:+AllowEnhancedClassRedefinition'
    )
}
